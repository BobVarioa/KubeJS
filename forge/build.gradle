loom {
	accessWidenerPath = project(":common").file("src/main/resources/kubejs.accesswidener")
	forge {
		convertAccessWideners = true
		extraAccessWideners.add("kubejs.accesswidener")
		mixinConfigs = [
				"kubejs-forge.mixins.json",
				"kubejs-common.mixins.json"
		]
	}
}

repositories {
	maven {
		name "ModMaven (JEI)"
		url "https://modmaven.dev/"
		content {
			includeGroup "mezz.jei"
			includeGroup "net.darkhax.bookshelf"
			includeGroup "net.darkhax.gamestages"
		}
	}
}

dependencies {
	forge("net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}")

	modApi("dev.architectury:architectury-forge:${rootProject.architectury_version}")

	modApi("dev.latvian.mods:rhino-forge:${rootProject.rhino_version}") { transitive false }

	// modLocalRuntime modCompileOnly("net.darkhax.bookshelf:Bookshelf-1.16.5:${rootProject.forge_bookshelf_version}")
	// modLocalRuntime modCompileOnly("net.darkhax.gamestages:GameStages-1.16.5:${rootProject.forge_gamestages_version}")

	// no reason to compile against the JEI API since REI already provides it,
	// but if you want to use it at runtime, you can use this next line and remove the modLocalRuntime from REI.
	// modLocalRuntime("mezz.jei:jei-1.18:${rootProject.forge_jei_version}")
	modLocalRuntime modCompileOnly("me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}")

	implementation(project(path: ":common", configuration: "namedElements")) { transitive false }
	bundle(project(path: ":common", configuration: "namedElements")) { transitive false }
}

processResources {
	def toReplace = [
			"version"             : project.version,
			"rhino_version"       : rootProject.rhino_version,
			"architectury_version": rootProject.architectury_version
	]

	inputs.properties toReplace
	filesMatching("META-INF/mods.toml") {
		expand toReplace
	}
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id
			releaseType = project.artifact_type
			addGameVersion "Forge"
			addGameVersion "1.18.2"
			mainArtifact(remapJar.archivePath)
			relations {
				requiredDependency 'rhino'
				requiredDependency 'architectury-api'
				optionalDependency 'roughly-enough-items'
				optionalDependency 'jei'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}

if(ENV.MODRINTH_TOKEN) {
	modrinth {
		token = ENV.MODRINTH_TOKEN
		projectId = rootProject.modrinth_id
		versionType = "$rootProject.artifact_type"
		versionNumber = "$project.version+$project.name"
		changelog = getGitChangelog()
		uploadFile = remapJar
		gameVersions = ["1.18.2"]
		loaders = ["forge"]
		dependencies {
			required.project('lhGA9TYQ') // arch
			required.project('sk9knFPE') // rhino
		}
	}
}

publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = "${rootProject.archives_base_name}-${project.name}"
			from components.java
		}
	}

	repositories {
		if (ENV.FTB_MAVEN_TOKEN) {
			maven {
				url "https://maven.ftb.dev/release"
				credentials {
					username = "latvian"
					password = "${ENV.FTB_MAVEN_TOKEN}"
				}
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/minecraft"
				credentials {
					username = "latvian"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}
